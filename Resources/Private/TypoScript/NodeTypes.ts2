prototype(Sfi.Listable:List) < prototype(Sfi.Listable:List) {
	@override.configuration = ${Configuration.setting('Sfi.Listable.list')}
	@override.configuration.@process.mergeConfiguration = ${NodeConfiguration.mergeWithNodeProperties(value, node)}
	@override.rootNode = ${q(node).property('rootNode') ? q(node).property('rootNode') : documentNode}

	prototype(Sfi.Listable:Listable) {
		collection.getFromRootNode {
			condition = ${true}
			renderer = ${q(rootNode).children('[instanceof Sfi.Listable:ListableMixin]').get()}
		}
	}

	@cache {
		mode = 'cached'
		entryIdentifier {
			node = ${node}
			editPreviewMode = ${editPreviewMode}
		}
		entryTags {
			1 = ${'Node_' + node.identifier}
			2 = ${'Node_' + documentNode.identifier}
			3 = ${'Node_' + rootNode.identifier}
			4 = ${'DescendantOf_' + rootNode.identifier}
		}
	}
	@process.contentElementWrapping = TYPO3.Neos:ContentElementWrapping
}
