prototype(Flowpack.Listable:PaginationArray) {
    @class = 'Flowpack\\Listable\\Fusion\\PaginationArrayImplementation'
    currentPage = ''
    maximumNumberOfLinks = ''
    totalCount = ''
    itemsPerPage = ''
}

prototype(Flowpack.Listable:PaginationParameters) < prototype(Neos.Fusion:RawArray)

prototype(Flowpack.Listable:Pagination) < prototype(Neos.Fusion:Component) {
    totalCount = 'to-be-set'
    maximumNumberOfLinks = 15
    itemsPerPage = 24

    @process.tmpl = ${'<nav>' + value + '</nav>'}
    class = 'pagination'
    itemClass = 'page-item'
    linkClass = 'page-link'
    currentItemClass = 'active'
    currentPage = ${request.arguments.currentPage || 1}

    renderer = Neos.Fusion:Collection {
        @if.paginationNeeded = ${(props.totalCount/props.itemsPerPage) > 1}
        @process.tmpl = ${'<ul class="' + props.class + '">' + value + '</ul>'}
        collection = Flowpack.Listable:PaginationArray {
            currentPage = ${props.currentPage}
            maximumNumberOfLinks = ${props.maximumNumberOfLinks}
            totalCount = ${props.totalCount}
            itemsPerPage = ${props.itemsPerPage}
        }
        itemName = 'i'
        itemRenderer = Neos.Fusion:Case {
            separator {
                condition = ${i == '...'}
                renderer = ${'<li class="' + props.itemClass + '"><a class="' + props.linkClass + '">' + i + '</a></li>'}
            }
            currentPage {
                condition = ${String.toInteger(i) == String.toInteger(props.currentPage)}
                renderer = ${'<li class="' + props.itemClass + ' ' + props.currentItemClass + '"><a class="' + props.linkClass + '">' + i + '</a></li>'}
            }
            link {
                condition = ${true}
                renderer = Neos.Fusion:Tag {
                    @process.tmpl = ${'<li class="' + props.itemClass + '">' + value + '</li>'}
                    tagName = 'a'
                    attributes {
                        href = Neos.Neos:NodeUri {
                            node = ${documentNode}
                            additionalParams = Flowpack.Listable:PaginationParameters {
                                currentPage = ${i}
                            }
                        }
                        class = ${props.linkClass}
                    }
                    content = ${i}
                }
            }
        }
    }
}
