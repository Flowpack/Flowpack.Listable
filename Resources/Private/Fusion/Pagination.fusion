prototype(Flowpack.Listable:PaginationArray) {
    @class = 'Flowpack\\Listable\\Fusion\\PaginationArrayImplementation'
    currentPage = ''
    maximumNumberOfLinks = ''
    totalCount = ''
    itemsPerPage = ''
    showPreviousNextLinks = false
}

prototype(Flowpack.Listable:PaginationParameters) < prototype(Neos.Fusion:RawArray)

prototype(Flowpack.Listable:Pagination) < prototype(Neos.Fusion:Component) {
    totalCount = 'to-be-set'
    maximumNumberOfLinks = 15
    itemsPerPage = 24
    showPreviousNextLinks = false

    class = 'Pagination'
    itemClass = 'Pagination-item'
    currentItemClass = 'isCurrent'
    currentPage = ${request.arguments.currentPage || 1}

    renderer = Neos.Fusion:Collection {
        @if.paginationNeeded = ${(props.totalCount/props.itemsPerPage) > 1}
        @process.tmpl = ${'<ul class="' + props.class + '">' + value + '</ul>'}
        collection = Flowpack.Listable:PaginationArray {
            currentPage = ${props.currentPage}
            maximumNumberOfLinks = ${props.maximumNumberOfLinks}
            totalCount = ${props.totalCount}
            itemsPerPage = ${props.itemsPerPage}
            showPreviousNextLinks = ${props.showPreviousNextLinks}
        }
        itemName = 'i'
        itemRenderer = Neos.Fusion:Case {
            separator {
                condition = ${i == '...'}
                renderer = ${'<li class="' + props.itemClass + '">' + i + '</li>'}
            }
            currentPage {
                condition = ${String.toInteger(i) == String.toInteger(props.currentPage)}
                renderer = ${'<li class="' + props.itemClass + ' ' + props.currentItemClass + '"><a>' + i + '</a></li>'}
            }
            previous {
                condition = ${i == 'previous'  && (props.showPreviousNextLinks == true)}
                renderer = Neos.Fusion:Tag {
                    @process.tmpl = ${'<li class="previous">' + value + '</li>'}
                    tagName = 'a'
                    attributes.href = Neos.Neos:NodeUri {
                        node = ${documentNode}
                        additionalParams = Flowpack.Listable:PaginationParameters {
                            currentPage = ${String.toInteger(props.currentPage) - 1}
                        }
                    }
                    content = ${Translation.translate(i, i, [], 'Main', 'Flowpack.Listable') }
                }
            }
            next {
                condition = ${i == 'next' && (props.showPreviousNextLinks == true)}
                renderer = Neos.Fusion:Tag {
                    @process.tmpl = ${'<li class="next">' + value  + '</li>'}
                    tagName = 'a'
                    attributes.href = Neos.Neos:NodeUri {
                        node = ${documentNode}
                        additionalParams = Flowpack.Listable:PaginationParameters {
                            currentPage = ${String.toInteger(props.currentPage) + 1}
                        }
                    }
                    content = ${Translation.translate(i, i, [], 'Main', 'Flowpack.Listable') }
                }
            }
            link {
                condition = ${(iterator.isFirst == false && iterator.isLast == false) || (props.showPreviousNextLinks == false)}
                renderer = Neos.Fusion:Tag {
                    @process.tmpl = ${'<li class="' + props.itemClass + '">' + value + '</li>'}
                    tagName = 'a'
                    attributes.href = Neos.Neos:NodeUri {
                        node = ${documentNode}
                        additionalParams = Flowpack.Listable:PaginationParameters {
                            currentPage = ${i}
                        }
                    }
                    content = ${i}
                }
            }
        }
    }
}
